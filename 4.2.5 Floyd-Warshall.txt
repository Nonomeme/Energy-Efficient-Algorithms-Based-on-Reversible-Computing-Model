procedure main()
    int nodes = 5
    int W[nodes][nodes] = {{0,3,8,0,-4},{0,0,0,1,7},{0,4,0,0,0},{2,0,-5,0,0},{0,0,0,6,0}}
    int D[nodes][nodes] = {{0}}
    int P[nodes][nodes] = {{0}}
    int garbage[nodes*nodes*nodes*2] = {0}
    int garbagecounter = 0
    
    call Floyd_Warshall(W, D, P, garbage, garbagecounter)

procedure Floyd_Warshall(int W[], int D[], int P[], int garbage[], int garbagecounter)
    local int k = 0, int nodes_size = size(W)
    call initialize_result(W,D,P)
    
    from k = 0 do
        local int i = 0
        from i = 0 do
            local int j = 0
            from j = 0 do
                if D[i][j] > D[i][k] + D[k][j] then
                    garbage[garbagecounter] += P[i][j] - P[k][j]  
                    P[i][j] -= garbage[garbagecounter]
                    garbagecounter += 1
                fi D[i][j] > D[i][k] + D[k][j]
                
                if D[i][j] >= D[i][k] + D[k][j] then
                    garbage[garbagecounter] += D[i][j] - (D[i][k] + D[k][j])
                    garbagecounter += 1
                    D[i][j] -= garbage[garbagecounter-1]
                fi D[i][j] = D[i][k] + D[k][j]
                j += 1
            until j = nodes_size
            delocal int j = nodes_size
            i += 1
        until i = nodes_size
        
        delocal int i = nodes_size
        k += 1
    until k = nodes_size
    
    delocal int k = size(W), int nodes_size = size(W)
    
procedure initialize_result(int W[], int D[], int P[])
    local int i = 0, int nodes = size(W)
    from i = 0 do
        local int j = 0
        from j = 0 do
            if i != j && W[i][j] = 0 then
                D[i][j] += 4294967295
            else
                D[i][j] += W[i][j]
            fi i != j && W[i][j] = 0
            
            if i != j && D[i][j] != 4294967295 then
                P[i][j] += i + 1
            fi  i != j && D[i][j] != 4294967295
            j += 1
        until j = nodes
        delocal int j = nodes
        i += 1
    until i = nodes
    delocal int i = size(W), int nodes = size(W)